
# How to: Highlight the Active Cell, Row, or Column

 **Last modified:** July 18, 2011

 _**Applies to:** Excel 2013 | Office 2013 | VBA_

 **In this article**
 [Highlighting the Active Cell](#sectionSection0)
 [Highlighting the Entire Row and Column that Contain the Active Cell](#sectionSection1)
 [Highlighting the Row and Column that Contain the Active Cell, Within the Current Region](#sectionSection2)
 [About the Contributor](#AboutContributor)


The following code examples show ways to highlight the active cell or the rows and columns that contain the active cell. These examples use the  ** [SelectionChange](183e2ca7-06b2-f689-1f77-182dbfbf1e1d.md)** event of the ** [Worksheet](182b705e-854a-81cc-a4b0-59b942de55ae.md)** object.
 **Sample code provided by:**
![MVP Contributor](images/odc_OfficeTA_33px_MVPContrib.jpg) Tom Urtis, [Atlas Programming Management](http://www.atlaspm.com/) | [About the Contributor](51a30ffb-77f2-4bd7-8eb6-b6781dc55d43.md#AboutContributor)

## Highlighting the Active Cell
<a name="sectionSection0"> </a>

The following code example clears the color in all the cells on the worksheet by setting the  ** [ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** property equal to 0, and then highlights the active cell by setting the **ColorIndex** property equal to 8 (Turquoise).


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    Application.ScreenUpdating = False
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    ' Highlight the active cell
    Target.Interior.ColorIndex = 8
    Application.ScreenUpdating = True
End Sub
```


## Highlighting the Entire Row and Column that Contain the Active Cell
<a name="sectionSection1"> </a>

The following code example clears the color in all the cells on the worksheet by setting the  ** [ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** property equal to 0, and then highlights the entire row and column that contain the active cell by using the ** [EntireRow](9e66da51-6cef-4109-ea4e-2acaad42aa1f.md)** and ** [EntireColumn](7be55670-75fd-fb02-dc1a-9d70e3a9d80d.md)** properties.


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    If Target.Cells.Count > 1 Then Exit Sub
    Application.ScreenUpdating = False
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    With Target
        ' Highlight the entire row and column that contain the active cell
        .EntireRow.Interior.ColorIndex = 8
        .EntireColumn.Interior.ColorIndex = 8
    End With
    Application.ScreenUpdating = True
End Sub
```


## Highlighting the Row and Column that Contain the Active Cell, Within the Current Region
<a name="sectionSection2"> </a>

The following code example clears the color in all the cells on the worksheet by setting the  ** [ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** property equal to 0, and then highlights the row and column that contain the active cell, within the current region by using the ** [CurrentRegion](39277cc5-07ff-8453-7330-b272b365f9dc.md)** property of the ** [Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** object.


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    If IsEmpty(Target) Or Target.Cells.Count > 1 Then Exit Sub
    Application.ScreenUpdating = False
    With ActiveCell
        ' Highlight the row and column that contain the active cell, within the current region
        Range(Cells(.Row, .CurrentRegion.Column), Cells(.Row, .CurrentRegion.Columns.Count + .CurrentRegion.Column - 1)).Interior.ColorIndex = 8
        Range(Cells(.CurrentRegion.Row, .Column), Cells(.CurrentRegion.Rows.Count + .CurrentRegion.Row - 1, .Column)).Interior.ColorIndex = 8
    End With
    Application.ScreenUpdating = True
End Sub
```


## About the Contributor
<a name="AboutContributor"> </a>

MVP Tom Urtis is the founder of Atlas Programming Management, a full-service Microsoft Office and Excel business solutions company in Silicon Valley. Tom has over 25 years of experience in business management and developing Microsoft Office applications, and is the co-author of "Holy Macro! It's 2,500 Excel VBA Examples." 


****   **Contribute to this article**Want to edit or suggest changes to this content? You can edit and submit changes to  [this article](https://github.com/jhershey00/VBA_Excel_Test/OpenXMLCon/articles/51a30ffb-77f2-4bd7-8eb6-b6781dc55d43.md) using GitHub.

